// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
	// Overwrite register if any of the control bits are on
	Or(a=reset, b=load, out=l1);
	Or(a=l1, b=inc, out=l);

	// Determine which value to feed into register
	Inc16(in=lo, out=oinc);
	Mux16(a=lo, b=oinc, sel=inc, out=o1);
	Mux16(a=o1, b=in, sel=load, out=o2);
	Mux16(a=o2, b=false, sel=reset, out=o3);

	Register(in=o3, load=l, out=lo, out=out);
}
